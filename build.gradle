import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
	id 'fabric-loom' version '1.3-SNAPSHOT'
	id 'maven-publish'
	id 'java'
	id "org.jetbrains.kotlin.jvm" version "1.9.10"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

test {
	useJUnitPlatform()
}

repositories {
	mavenLocal()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

	// External dependencies
	implementation include("com.fasterxml.jackson.core:jackson-databind:$jackson_version")
	implementation include("com.fasterxml.jackson.core:jackson-annotations:$jackson_version")
	implementation include("com.fasterxml.jackson.core:jackson-core:$jackson_version")
	implementation include("org.docopt:docopt:$docopt_version")

	// Test dependencies
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

tasks.withType(KotlinCompile).configureEach {
	kotlinOptions {
		jvmTarget = 17
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

tasks.register('generateBuildProperties') {
	dependsOn processResources
	doLast {
		new File("$buildDir/resources/main/build.properties").withWriter { writer ->
			Properties p = new Properties()
			p['mod_version'] = project.version.toString()
			p.store writer, null
		}
	}
}

jar {
	dependsOn generateBuildProperties
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
